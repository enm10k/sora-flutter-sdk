group 'jp.shiguredo.sora_flutter_sdk'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.6.10'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

task download(type:Exec) {
    commandLine 'python3', "$projectDir/install_deps.py", "android", "--webrtc-version", "m105.5195.0.0", "--boost-version", "1.80.0", "--sora-version", "2022.11.1"
}
preBuild.dependsOn download

android {
    compileSdkVersion 31
    ndkVersion "25.1.8937393"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        minSdkVersion 16

        externalNativeBuild {
            cmake {
                cppFlags '-std=gnu++17'
                arguments "-DSORA_DIR=$projectDir/_install/sora",
                    "-DCMAKE_BUILD_TYPE=Release",
                    "-DBOOST_ROOT=$projectDir/_install/boost",
                    "-DBoost_LIBRARY_DIRS=$projectDir/_install/boost/lib",
                    "-DWEBRTC_INCLUDE_DIR=$projectDir/_install/webrtc/include",
                    "-DWEBRTC_LIBRARY_DIR=$projectDir/_install/webrtc/lib/arm64-v8a",
                    "-DLIBCXX_INCLUDE_DIR=$projectDir/_install/llvm/libcxx/include",
                    "-DANDROID_STL=none",
                    "-DANDROID_NATIVE_API_LEVEL=29",
                    "-DANDROID_PLATFORM=29",
                    "-DANDROID_ABI=arm64-v8a",
                    "-DANDROID_CPP_FEATURES=exceptions rtti",
                    "-DCMAKE_ANDROID_EXCEPTIONS=ON"
            }
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation fileTree(dir: "$projectDir/_install/webrtc/jar/", include: ['webrtc.jar'])
}
